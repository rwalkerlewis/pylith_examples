
# ----------------------------------------------------------------------
# Create quad mesh at 4 km resolution.

# ----------------------------------------------------------------------
# Set units to SI.
# ----------------------------------------------------------------------
${Units('si')}
#
# ----------------------------------------------------------------------
# Reset geometry.
# ----------------------------------------------------------------------
reset

# Make sure undo is off to prevent errors in stitching volumes.
undo off

# Turn on IDless journal files for maximum compatibility.
# The commands echoed to the log will replace the object ID with a
# geometric specification of the object. Note that this means changing
# the geometry will require changes to subsequent journal commands.
journal idless on

# ----------------------------------------------------------------------
# Create block
# ----------------------------------------------------------------------
# Block is 150.0 km x 100.0 km
# -50.0 km <= x <= 50.0 km
# -75.0 km <= y <= 75.0 km
#
${blockWidth=100*km}
${blockLength=150*km}
${blockHeight=10*km}

${faultLength=150.0*km}

brick x {blockWidth} y {blockLength} z {blockHeight}
${idV=Id("volume")}

# surface 1 name "surf_front"
surface ( at 0 0 5000 ordinal 1 ordered ) name "surf_front"

# surface 2 name "surf_back"
surface ( at 0 0 -5000 ordinal 1 ordered ) name "surf_back"

create midsurface volume {idV} surface surf_front surf_back
${idS=Id("surface")}
delete volume {idV}

# ----------------------------------------------------------------------
# Create interface surfaces
# ----------------------------------------------------------------------

# Create fault (yz plane) at x = 0.0
split surface {idS} across location position 0 {-blockLength/2.0} 0 location position 0 {blockLength/2.0} 0

# curve 17 name "fault_surface"
curve ( at 0 0 0 ordinal 1 ordered ) name "fault_surface"

# ----------------------------------------------------------------------
# Name surfaces
# ----------------------------------------------------------------------
# surface 8 name "surface_xpos"
surface  ( at 25000 0 0 ordinal 1 ordered )  name "surface_xpos"

# surface 9 name "surface_xneg"
surface  ( at -25000 0 0 ordinal 1 ordered )  name "surface_xneg"

# ----------------------------------------------------------------------
# Name curves
# ----------------------------------------------------------------------

# curve 19 name "edge_ypos_xpos"
curve ( at 25000 75000 0 ordinal 1 ordered ) name "edge_ypos_xpos"

# curve 20 name "edge_ypos_xneg"
curve ( at -25000 75000 0 ordinal 1 ordered ) name "edge_ypos_xneg"

# curve 18 name "edge_yneg_xpos"
curve ( at 25000 -75000 0 ordinal 1 ordered ) name "edge_yneg_xpos"

# curve 21 name "edge_yneg_xneg"
curve ( at -25000 -75000 0 ordinal 1 ordered ) name "edge_yneg_xneg"

# curve 14 name "edge_xpos"
curve ( at 50000 0 0 ordinal 1 ordered ) name "edge_xpos"

# curve 16 name "edge_xneg"
curve ( at -50000 0 0 ordinal 1 ordered ) name "edge_xneg"

# ----------------------------------------------------------------------
# Name vertices
# ----------------------------------------------------------------------

# vertex 14 name "vertex_fault_ypos"
vertex ( at 0 75000 0 ordinal 1 ordered ) name "vertex_fault_ypos"

# vertex 13 name "vertex_fault_yneg"
vertex ( at 0 -75000 0 ordinal 1 ordered ) name "vertex_fault_yneg"

# vertex 11 name "vertex_ypos_xpos"
vertex ( at 50000 75000 0 ordinal 1 ordered ) name "vertex_ypos_xpos"

# vertex 12 name "vertex_ypos_xneg"
vertex ( at -50000 75000 0 ordinal 1 ordered ) name "vertex_ypos_xneg"

# vertex 9 name "vertex_yneg_xpos"
vertex ( at 50000 -75000 0 ordinal 1 ordered ) name "vertex_yneg_xpos"

# vertex 10 name "vertex_yneg_xneg"
vertex ( at -50000 -75000 0 ordinal 1 ordered ) name "vertex_yneg_xneg"

# ----------------------------------------------------------------------
# Set discretization size and scheme
#
# The "pave" scheme is an unstructured 2-D quad meshing scheme.
# ----------------------------------------------------------------------
${dx=4.0*km}
surface all scheme pave

# ----------------------------------------------------------------------
# Set vertex spacing with increasing spacing away from fault
# ----------------------------------------------------------------------
${bias_factor=1.05}

# Function for computing discretization size at end of curve with bias
${fun_dx='dxStart*bias_factor**ceil( ln(1-curveL/dxStart*(1-bias_factor))/ln(bias_factor))'}

# ----------------------------------------------------------------------
# Compute sizes at curve endpoints
# ----------------------------------------------------------------------

# dxA - size at vertex_ypos_xpos, vertex_ypos_xneg, vertex_yneg_xpos, vertex_yneg_xneg
${dxStart=dx}{curveL=Length(Id("edge_ypos_xneg"))}{execute("dxA="//fun_dx)}

# ----------------------------------------------------------------------
# Reset sizes
# ----------------------------------------------------------------------
curve all scheme default
surface all sizing function none

# Set size on faults
curve fault_surface size {dx}

# Fault to edge
curve edge_ypos_xneg scheme bias fine size {dx} factor {bias_factor} start vertex vertex_fault_ypos
curve edge_ypos_xpos scheme bias fine size {dx} factor {bias_factor} start vertex vertex_fault_ypos
curve edge_yneg_xneg scheme bias fine size {dx} factor {bias_factor} start vertex vertex_fault_yneg
curve edge_yneg_xpos scheme bias fine size {dx} factor {bias_factor} start vertex vertex_fault_yneg

# Mesh edges
curve edge_xneg size {dxA}
curve edge_xpos size {dxA}

# For CUBIT 15.4
set trimesher surface gradation {bias_factor}

# For earlier versions of CUBIT
surface all sizing function type bias start curve fault_surface factor {bias_factor}

# ----------------------------------------------------------------------
# Generate the mesh
# ----------------------------------------------------------------------
# preview mesh surface all
mesh surface all

# ----------------------------------------------------------------------
# Smooth mesh to improve quality
# ----------------------------------------------------------------------
surface all smooth scheme condition number beta 1.1 cpu 10
smooth surface all

# ----------------------------------------------------------------------
# Setup boundary conditions.

# ----------------------------------------------------------------------
# Create blocks for materials
# ----------------------------------------------------------------------
block 1 surface surface_xneg
block 1 name "elastic_xneg"

block 2 surface surface_xpos
block 2 name "elastic_xpos"

# ----------------------------------------------------------------------
# Create nodeset for fault
# ----------------------------------------------------------------------
group "fault" add node in fault_surface
nodeset 10 group fault
nodeset 10 name "fault"

# ----------------------------------------------------------------------
# Create nodeset for +x face
# ----------------------------------------------------------------------
group "face_xpos" add node in curve edge_xpos
nodeset 21 group face_xpos
nodeset 21 name "face_xpos"

# ----------------------------------------------------------------------
# Create nodeset for -x face
# ----------------------------------------------------------------------
group "face_xneg" add node in curve edge_xneg
nodeset 22 group face_xneg
nodeset 22 name "face_xneg"

# ----------------------------------------------------------------------
# Create nodeset for +y face
# ----------------------------------------------------------------------
group "face_ypos" add node in curve edge_ypos_xneg
group "face_ypos" add node in curve edge_ypos_xpos
nodeset 23 group face_ypos
nodeset 23 name "face_ypos"

# ----------------------------------------------------------------------
# Create nodeset for -y face
# ----------------------------------------------------------------------
group "face_yneg" add node in curve edge_yneg_xneg
group "face_yneg" add node in curve edge_yneg_xpos
nodeset 24 group face_yneg
nodeset 24 name "face_yneg"

# ----------------------------------------------------------------------
# Export exodus file
# ----------------------------------------------------------------------
export mesh "mesh_quad.exo" dimension 2 overwrite


# End of file






